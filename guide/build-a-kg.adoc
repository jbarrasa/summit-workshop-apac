== Building a KG

We will build the KG in two steps: first we load the data and then we overlay the organising principle (the concept scheme or ontology).

Once the KG is built, we'll run a few semantic searches on it.

== Loading the data: Articles with pre-extracted entities

Use the https://data-importer.graphapp.io/[data importer] with https://github.com/jbarrasa/summit-workshop-apac/blob/main/other/imodel.json[this model]
and https://github.com/jbarrasa/summit-workshop-apac/tree/main/data[this set of data files].


== Some algos?

[source,cypher]
----
CALL gds.graph.project(
    'articleSimGraph',
    ['Article', 'Topic'],
    {
        genre: {
            type: 'about' 
        }
    }
);
----

[source,cypher]
----
CALL gds.nodeSimilarity.stream('articleSimGraph')
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).title AS article1, gds.util.asNode(node2).title AS article2, similarity
ORDER BY similarity DESCENDING, article1, article2
---

== Ontology Import

=== Configure the DB so that it's ready to import RDF data

We first create the required constraint

[source,cypher]
----
CREATE CONSTRAINT n10s_unique_uri ON (r:Resource) ASSERT r.uri IS UNIQUE;
----

And set the config for importing ontologies:

[source,cypher]
----
call n10s.graphconfig.init({ handleVocabUris: "IGNORE", classLabel: "Topic", subClassOfRel: "broader"});
----

Add extra tag to `Topic` nodes so that the linkage happens automatically
[source,cypher]
----
MATCH (t:Topic) set t:Resource
----


=== Preview a smaller version of the concept scheme

[source,cypher]
----
call n10s.rdf.preview.fetch("https://raw.githubusercontent.com/jbarrasa/summit-workshop-apac/main/ontos/concept-scheme-skos-lite.ttl","Turtle")
----

=== Let's import the concept scheme

[source,cypher]
----
call n10s.skos.import.fetch("https://raw.githubusercontent.com/jbarrasa/goingmeta/main/session2/resources/goingmeta-skos.ttl","Turtle");
----

Let's 'clean' the taxonomy: remove redundant `broader` relationships (from Wikidata extract).

[source,cypher]
----
match (s:Topic)-[shortcut:broader]->(:Topic)<-[:broader*2..]-(s)
delete shortcut;
----

== Some Semantic searches on the KG (1)

__Articles on "NoSQL database management system"__

This will require returning articles both directly or indirectly connected to that category.

[source,cypher]
----
MATCH path = (:Topic {prefLabel: "NoSQL database management system"})<-[:broader*0..]-(sc)<-[:about]-(art:Article)
return art.title, [x in nodes(path) where x:Topic | coalesce(x.prefLabel,"") + coalesce(x.label,"") ] as explanation
----

or using the inferencing method in n10s

[source,cypher]
----
match (c:Topic {prefLabel: "NoSQL database management system"})
call n10s.inference.nodesInCategory(c, { inCatRel: "about"}) yield node as article
return article.title as result
----

== Some Semantic searches on the KG (2)

__Read next__ type of queries for recommendation/personalisation.

Use these queries to create a search phrase in Bloom and visually explore recommendation paths.


Article by title: `$title`

[source,cypher]
----
match (a:Article) where a.title = $title return a
----


[source,cypher]
----
match simpath = (a:Article)-[:about]->(cat)-[:broader*0..1]->()<-[:broader*0..1]-()<-[:about]-(other)
where a.uri = "https://dev.to/qainsights/performance-testing-neo4j-database-using-bolt-protocol-in-apache-jmeter-1oa9"
return other.title, [x in nodes(simpath) where x:Topic | coalesce(x.prefLabel,x.label) ] as explain
----

The quality of the results can be improved by adding another ontology (software stack onto)

[source,cypher]
----
CALL n10s.onto.import.fetch("http://www.nsmntx.org/2020/08/swStacks","Turtle");
----

